/**
 * build.gradle
 * Build file for the JSON/Jackson Samples Project
 */
// Plugins
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

// Force Java 8
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Look for a local gradle setup file, and include it
if( file('local.gradle').exists() ){
	apply from: 'local.gradle'
}

// Build options/setup for this project
project.ext {
	description = 'JSON/Jackson Samples Project'
	version = '0.01'
	status = 'alpha'
}

// Repositories
repositories {
	mavenLocal()
    mavenCentral()
}

// Local publishing
publishing {
   repositories {
       mavenLocal()
   }
   publications {
       impl(MavenPublication) {
           groupId 'net.ourpts.json'
           artifactId 'Jackson'
           version "${project.ext.version}"

           artifact JsonJacksonJar
       }
   }
}

// Project dependencies
dependencies {
	// Our JSON/Common code
	compile(group: 'net.ourpts.json', name: 'Common', version: '+' )

	// JSON Support via Jackson by FasterXML
	compile(group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '+')
	compile(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '+')
	compile(group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '+')
}

/*
 * Task configuration
 */
clean {
	delete 'build'
	delete 'dist'
}

task JsonJacksonJar (type: Jar, dependsOn: classes) {
	from sourceSets.main.output
	include 'net/ourpts/json/jackson/'
	baseName = "ourpts-json_jackson-${project.ext.version}"
	destinationDir = file('dist')
	manifest{
		attributes('Main-Class':'net.ourpts.json.jackson.JacksonSample')
	}
}
artifacts {
	archives JsonJacksonJar
}

javadoc {
	source = sourceSets.main.allJava
	destinationDir = file('dist/javadoc')
}

task javadocJar (type: Jar, dependsOn: javadoc) {
	from javadoc.destinationDir
	baseName = "ourpts-json_jackson-${project.ext.version}"
	destinationDir = file('dist')
	classifier = 'javadoc'
}

tasks.build.dependsOn( [ 'JsonJacksonJar', 'javadocJar' ] )

// By default, build our JAR and Javadoc JAR
defaultTasks 'JsonJacksonJar', 'javadocJar'

// END: build.gradle
